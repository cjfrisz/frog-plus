#!/usr/local/bin/zsh

# Change these two variable to match the hostname and path for the
# deploy subcommand
host=webhost
website_path=www

# Change this to match the value out of "output-dir" in .frogrc
blog_dir=blog

new_post_tmp=.new-post.tmp
draft=.draft

function frog_fixup () {
  # copy resource directories into blog directory
  foreach d ( css img js )
    rsync -az $d $blog_dir
  end

  # fix references
  find $blog_dir -name '*.html' -exec sed -i.0 -E "s/(href=\"\/)(css|feeds|tags|[0-9]{4})/\1$blog_dir\/\2/g" {} \; 
  find $blog_dir -name '*.html' -exec sed -i.0 -E "s/(src=\"\/)(js)/\1$blog_dir\/\2/g" {} \; 
  find $blog_dir -name '*.html' -exec sed -i.0 -E "s/((About|index(-[0-9]+)?).html)/$blog_dir\/\1/g" {} \;
  find $blog_dir -name '*.html' -exec sed -i.0 -E "s/(\/$blog_dir){2}/\1/g" {} \;
  find $blog_dir -name '*.html.0' -exec rm {} \;
}

function frog_command () {
  case "$1" in
    build)
      echo building blog to output $blog_dir
      raco frog -b
      ;;
    preview)
      raco frog -p
      ;;
    fixup)
      if [[ $blog_dir != "." ]] ; then
        echo fixing up references
        frog_fixup
      fi
      ;;
    deploy)
      echo deploying
      rsync -avz --exclude '_src' $blog_dir $host:$website_path
      ;;
    clean)
      if [[ $blog_dir == "." ]] ; then
        raco frog -c
        rm -f index-*.html sitemap.txt
      else 
        # If using an output directory other than the blog's top level,
        # it's easier to just blow away the whole blog directory
	echo deleting $blog_dir
	rm -rf $blog_dir
      fi
      ;;
    resume)
      if [[ -e $draft ]] ; then
	$EDITOR `readlink $draft`
      else
        echo no draft to resume
      fi
      ;;
    delete-draft)
      echo deleting draft `readlink $draft`
      rm -f `readlink $draft`
      rm -f $draft
      ;;
    init)
      raco frog --init
      ;;
    all)
      $0 build fixup
      ;;
    all-deploy)
      $0 build fixup deploy
      ;;
    *)
      exec raco frog "$@"
      ;;
  esac
}

if [[ "$@" == "" ]] ; then
  echo no subcommand specified;
  exit 1;
fi

case "$1" in 
  new)
    raco frog -n "$2" > $new_post_tmp
    post_name=`sed '1,2d' $new_post_tmp`
    rm -f $new_post_tmp
    ln -fs $post_name $draft
    exec $0 resume ${@:3}
    ;;
  *)
    foreach subcmd ( "$@" )
      frog_command $subcmd
    end
    ;;
esac
